load("@bazel-erlang//:bazel_erlang_lib.bzl", "app_file", "bazel_erlang_lib", "erlc")
load("@bazel-erlang//:ct.bzl", "ct_test")
load("//:rabbitmq.bzl", "RABBITMQ_ERLC_OPTS", "APP_VERSION")

genrule(
    name = "generated_headers",
    srcs = [
        "//deps/rabbitmq_codegen:amqp_codegen.py",
        "//deps/rabbitmq_codegen:amqp-rabbitmq-0.9.1.json",
        "//deps/rabbitmq_codegen:credit_extension.json",
        "//deps/rabbitmq_codegen:amqp-rabbitmq-0.8.json",
        "codegen.py",
    ],
    outs = ["include/rabbit_framing.hrl"],
    cmd = """
    env PYTHONPATH=$$(dirname $(location //deps/rabbitmq_codegen:amqp_codegen.py)) \\
        python3 $(location codegen.py) \\
        --ignore-conflicts \\
        header \\
        $(location //deps/rabbitmq_codegen:amqp-rabbitmq-0.9.1.json) \\
        $(location //deps/rabbitmq_codegen:credit_extension.json) \\
        $(location //deps/rabbitmq_codegen:amqp-rabbitmq-0.8.json) \\
        $@
    """,
)

genrule(
    name = "rabbit_framing_amqp_0_9_1.erl",
    srcs = [
        "//deps/rabbitmq_codegen:amqp_codegen.py",
        "//deps/rabbitmq_codegen:amqp-rabbitmq-0.9.1.json",
        "codegen.py",
    ],
    outs = ["src/rabbit_framing_amqp_0_9_1.erl"],
    cmd = """
    env PYTHONPATH=$$(dirname $(location //deps/rabbitmq_codegen:amqp_codegen.py)) \\
        python3 $(location codegen.py) \\
        body \\
        $(location //deps/rabbitmq_codegen:amqp-rabbitmq-0.9.1.json) \\
        $@
    """,
)

genrule(
    name = "rabbit_framing_amqp_0_8.erl",
    srcs = [
        "//deps/rabbitmq_codegen:amqp_codegen.py",
        "//deps/rabbitmq_codegen:amqp-rabbitmq-0.8.json",
        "codegen.py",
    ],
    outs = ["src/rabbit_framing_amqp_0_8.erl"],
    cmd = """
    env PYTHONPATH=$$(dirname $(location //deps/rabbitmq_codegen:amqp_codegen.py)) \\
        python3 $(location codegen.py) \\
        body \\
        $(location //deps/rabbitmq_codegen:amqp-rabbitmq-0.8.json) \\
        $@
    """,
)

DEPS = [
    "@lager//:bazel_erlang_lib",
]

RUNTIME_DEPS = [
    "@jsx//:bazel_erlang_lib",
    "@ranch//:bazel_erlang_lib",
    "@recon//:bazel_erlang_lib",
    "@credentials-obfuscation//:bazel_erlang_lib",
]

APP_NAME = "rabbit_common"

HDRS = glob(["include/*.hrl"]) + ["include/rabbit_framing.hrl"]

app_file(
    name = "app_file",
    app_name = APP_NAME,
    app_description = "Modules shared by rabbitmq-server and rabbitmq-erlang-client",
    app_version = APP_VERSION,
    extra_apps = [
        "compiler",
        "crypto",
        "public_key",
        "sasl",
        "ssl",
        "syntax_tools",
        "tools",
        "xmerl",
    ],
    modules = [":beam_files"],
    deps = DEPS + RUNTIME_DEPS,
)

FIRST_SRCS = [
    "src/gen_server2.erl",
    "src/rabbit_authn_backend.erl",
    "src/rabbit_authz_backend.erl",
    "src/rabbit_registry_class.erl",
]

erlc(
    name = "first_beam_files",
    hdrs = HDRS,
    srcs = glob(FIRST_SRCS),
    erlc_opts = RABBITMQ_ERLC_OPTS,
    dest = "ebin",
    deps = DEPS,
)

erlc(
    name = "beam_files",
    hdrs = HDRS,
    srcs = glob(["src/*.erl"], exclude = FIRST_SRCS) + ["src/rabbit_framing_amqp_0_8.erl", "src/rabbit_framing_amqp_0_9_1.erl"],
    erlc_opts = RABBITMQ_ERLC_OPTS,
    beam = [":first_beam_files"],
    dest = "ebin",
    deps = DEPS,
)

bazel_erlang_lib(
    name = "bazel_erlang_lib",
    app_name = APP_NAME,
    app_version = APP_VERSION,
    hdrs = HDRS,
    app = ":app_file",
    beam = [":first_beam_files", ":beam_files"],
    visibility = ["//visibility:public"],
)

erlc(
    name = "first_test_beam_files",
    hdrs = HDRS,
    srcs = glob(FIRST_SRCS),
    erlc_opts = RABBITMQ_ERLC_OPTS + [
        "-DTEST",
        "+debug_info",
    ],
    dest = "src",
    deps = DEPS,
    testonly = True,
)

erlc(
    name = "test_beam_files",
    hdrs = HDRS,
    srcs = glob(["src/*.erl"], exclude = FIRST_SRCS) + ["src/rabbit_framing_amqp_0_8.erl", "src/rabbit_framing_amqp_0_9_1.erl"],
    erlc_opts = RABBITMQ_ERLC_OPTS + [
        "-DTEST",
        "+debug_info",
    ],
    beam = [":first_test_beam_files"],
    dest = "src",
    deps = DEPS,
    testonly = True,
)

bazel_erlang_lib(
    name = "test_bazel_erlang_lib",
    app_name = APP_NAME,
    app_version = APP_VERSION,
    hdrs = HDRS,
    app = ":app_file",
    beam = [":first_test_beam_files", ":test_beam_files"],
    testonly = True,
)

erlc(
    name = "rabbit_env_SUITE_beam_files",
    hdrs = HDRS,
    srcs = ["test/rabbit_env_SUITE.erl"],
    dest = "test",
    deps = [":test_bazel_erlang_lib"] + DEPS,
    testonly = True,
)

ct_test(
    name = "rabbit_env_SUITE",
    suites = [":rabbit_env_SUITE_beam_files"],
    deps = [
        ":test_bazel_erlang_lib",
        "//deps/rabbit:bazel_erlang_lib",
        "@lager//:bazel_erlang_lib",
        "@proper//:bazel_erlang_lib",
    ],
    size = "small",
)

erlc(
    name = "supervisor2_SUITE_beam_files",
    hdrs = HDRS,
    srcs = ["test/supervisor2_SUITE.erl"],
    dest = "test",
    deps = [":test_bazel_erlang_lib"] + DEPS,
    testonly = True,
)

ct_test(
    name = "supervisor2_SUITE",
    suites = [":supervisor2_SUITE_beam_files"],
    deps = [
        ":test_bazel_erlang_lib",
    ],
    size = "small",
)

erlc(
    name = "unit_priority_queue_SUITE_beam_files",
    hdrs = HDRS,
    srcs = ["test/unit_priority_queue_SUITE.erl"],
    dest = "test",
    deps = [":test_bazel_erlang_lib"] + DEPS,
    testonly = True,
)

ct_test(
    name = "unit_priority_queue_SUITE",
    suites = [":unit_priority_queue_SUITE_beam_files"],
    deps = [
        ":test_bazel_erlang_lib",
    ],
    size = "small",
)

erlc(
    name = "unit_SUITE_beam_files",
    hdrs = HDRS,
    srcs = [
        "test/unit_SUITE.erl",
        "test/gen_server2_test_server.erl",
    ],
    dest = "test",
    deps = DEPS + [
        ":test_bazel_erlang_lib",
        "@proper//:bazel_erlang_lib",
    ],
    testonly = True,
)

ct_test(
    name = "unit_SUITE",
    suites = [":unit_SUITE_beam_files"],
    deps = [
        ":test_bazel_erlang_lib",
        "@proper//:bazel_erlang_lib",
        "@lager//:bazel_erlang_lib",
        "@credentials-obfuscation//:bazel_erlang_lib",
    ],
    size = "small",
)

erlc(
    name = "worker_pool_SUITE_beam_files",
    hdrs = HDRS,
    srcs = ["test/worker_pool_SUITE.erl"],
    dest = "test",
    deps = [":test_bazel_erlang_lib"] + DEPS,
    testonly = True,
)

ct_test(
    name = "worker_pool_SUITE",
    suites = [":worker_pool_SUITE_beam_files"],
    deps = [
        ":test_bazel_erlang_lib",
        "@lager//:bazel_erlang_lib",
    ],
    size = "small",
)
